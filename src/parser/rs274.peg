grammar RS274Line
  line <- "/"? line_number? ( segment )* [\n]? %make_line
  line_number <- [Nn] digit digit? digit? digit? digit? %make_line_number
  digit <- [0-9]
  segment <- mid_line_word / comment / parameter_setting
  mid_line_word <- mid_line_letter real_value %make_mid_line_word

  real_value <- real_number / expression / parameter_value / unary_combo
  real_number <- ( "+" / "-" )? ( (digit ( digit )* (".")? ( digit )*) / ("." digit ( digit )*) ) %make_real_number
  parameter_value <- "#" real_value %make_get_parameter_value
  unary_combo <- ordinary_unary_combo / arc_tangent_combo
  ordinary_unary_combo <- ordinary_unary_operation expression %make_ordinary_unary_combo
  arc_tangent_combo <- "atan" expression "/" expression %make_arc_tangent_combo
  expression <- "[" real_value ( binary_operation_combo )* "]" %make_expression

  parameter_setting <- "#" real_value "=" real_value
  binary_operation_combo <- ( binary_operation1 / binary_operation2 / binary_operation3 ) real_value %make_binary_operation_combo
  comment <- (message / ordinary_comment)
  ordinary_comment <- "(" (comment_character)* ")"
  message <- "(" ( white_space )* [Mm] ( white_space )* [Ss] ( white_space )* [Gg] ( white_space )* comma ( comment_character )* ")"

  ordinary_unary_operation <- "abs" / "acos" / "asin" / "cos" / "exp" / "fix" / "fup" / "ln" / "round" / "sin" / "sqrt" / "tan"
  binary_operation1 <- "**" %make_bin_op
  binary_operation2 <- "/" / "mod" / "*" %make_bin_op
  binary_operation3 <- "and" / "xor" / "-" / "or" / "+" %make_bin_op
  mid_line_letter <- [a-dA-Df-nF-Np-tP-Tx-zX-Z]
  comment_character <- [a-zA-Z0-9!"#$%&'*\+,.\/:;<=>?@\[\] ^_`{|}~\- \t]
  white_space <- [\t ]